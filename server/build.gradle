plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.20.0'
}

sourceCompatibility=17
targetCompatibility=17

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'littlehorse-server'
            version = version
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri("https://packages.confluent.io/maven/")
    }
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.junit-pioneer:junit-pioneer:2.0.0'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'
    testImplementation 'net.datafaker:datafaker:1.9.0'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'

    // Stuff for Kafka
    implementation "org.apache.kafka:kafka-clients:${kafkaVersion}"
    implementation "org.apache.kafka:kafka-streams:${kafkaVersion}"
    testImplementation "org.apache.kafka:kafka-streams-test-utils:${kafkaVersion}"

    // Utils
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.guava:guava:32.0.1-jre'

    // gRPC and Protobuf now included in lh-sdk
    implementation project(':sdk-java')

    // Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus:1.11.2'
    implementation 'io.javalin:javalin:5.6.0'

    // Lombok stuffs
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}


application {
    // Define the main class for the application.
    mainClass = 'io.littlehorse.App'
    executableDir = 'server'
}

compileJava {
    options.compilerArgs << '-parameters'
}

shadowJar {
    mergeServiceFiles()
}

test {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED', '--add-opens', 'java.base/java.util=ALL-UNNAMED']
}

spotless {
    java {
        googleJavaFormat('1.17.0').aosp().reflowLongStrings()
    }
}