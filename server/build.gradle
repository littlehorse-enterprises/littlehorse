plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

def defaultJvmArgs = [
    '-XX:+UseZGC'
]


javadoc {
    excludes = [
        'io/littlehorse/common/model/**',
        'io/littlehorse/server/streams/storeinternals/ReadOnlyGetableManager.java',
        'io/littlehorse/server/streams/taskqueue/OneTaskQueue.java',
        'io/littlehorse/common/util/LHUtil.java'
    ]
    options.memberLevel = JavadocMemberLevel.PUBLIC
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = 'littlehorse-server'
            version = version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'LittleHorse Server'
                description = 'LittleHorse Server. LittleHorse is a high-performance microservice orchestration engine that allows developers to build scalable, maintainable, and observable applications.'
                url = 'https://github.com/littlehorse-enterprises/littlehorse'
                licenses {
                    license {
                        name = 'GNU Affero General Public License Version 3'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        name = 'LittleHorse Engineering'
                        email = 'engineering@littlehorse.io'
                    }
                }
                scm {
                    url = 'https://github.com/littlehorse-enterprises/littlehorse'
                    connection = 'scm:git://github.com/littlehorse-enterprises/littlehorse.git'
                    developerConnection = 'scm:git:ssh://github.com/littlehorse-enterprises/littlehorse.git'
                }
                packaging = 'jar'
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") }

    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.mavenJava
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url = uri("https://packages.confluent.io/maven/")
    }
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')

    // Test tools
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.junit-pioneer:junit-pioneer:2.0.0'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    testImplementation 'net.datafaker:datafaker:1.9.0'
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"

    // Logging
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'

    // Stuff for Kafka
    implementation "org.apache.kafka:kafka-clients:4.2.0-SNAPSHOT"
    implementation "org.apache.kafka:kafka-streams:4.2.0-SNAPSHOT"
    // implementation "org.apache.kafka:kafka-clients:${kafkaVersion}"
    // implementation "org.apache.kafka:kafka-streams:${kafkaVersion}"
    testImplementation "org.apache.kafka:kafka-streams-test-utils:${kafkaVersion}"

    // Auth
    implementation "com.nimbusds:oauth2-oidc-sdk:${nimbusVersion}"

    // Utils
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation "commons-io:commons-io:${commonsioVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"

    // gRPC and Protobuf now included in lh-sdk
    implementation project(':sdk-java')

    // Prometheus
    implementation "io.micrometer:micrometer-registry-prometheus:${micrometerVersion}"

    // Lombok stuff
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation project(":test-utils")
    testImplementation project(":test-utils-container")

    // JSON and YAML processing
    implementation 'com.jayway.jsonpath:json-path:2.9.0'
    implementation 'com.google.code.gson:gson:2.12.1'

    // To run scheduled wfs
    implementation 'com.cronutils:cron-utils:9.2.1'

    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
}


application {
    // Define the main class for the application.
    mainClass = 'io.littlehorse.App'
    executableDir = 'server'
    applicationDefaultJvmArgs = defaultJvmArgs
}

compileJava {
    options.compilerArgs << '-parameters'
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes(
            'Main-Class': 'io.littlehorse.App',
            'JVM-Args': defaultJvmArgs.join(' ')
        )
    }
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching "io.littlehorse.**"
        excludeTestsMatching "e2e.**"
    }
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED', '--add-opens', 'java.base/java.util=ALL-UNNAMED']
}

task e2e(type: Test, dependsOn: compileJava){
    useJUnitPlatform()
    filter {
        includeTestsMatching "e2e.**"
        excludeTestsMatching "io.littlehorse.**"
    }
    testLogging {
        events 'passed', 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED', '-Xms8192m', '-Xmx8192m', '-Xss2m', '-XX:+UseParallelGC', '--add-opens', 'java.base/java.util=ALL-UNNAMED']

    // // Enables parallelism on the e2e tests
    // maxParallelForks = 2
    // systemProperty "junit.jupiter.execution.parallel.enabled", "true"
}

spotless {
    java {
        target('**/*.java')
        targetExclude('**/proto/*.java')
        palantirJavaFormat()
    }
}

task testJar(type: Jar) {
    from sourceSets.test.output
    archiveClassifier.set('tests')
    description = 'Assembles a jar archive containing the test classes.'
    group = 'build'
    manifest {
        attributes(
                'Class-Path': configurations.testRuntimeClasspath.files.collect { it.name }.join(' '),
                'Main-Class': 'org.junit.platform.console.ConsoleLauncher'
        )
    }

    dependsOn configurations.testRuntimeClasspath
    from {
        configurations.testRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
}


task staticVersion {
    doFirst {
        def outputDir = file("$buildDir/generated/sources/version");
        def versionClass = new File(outputDir, "io/littlehorse/server/ServerVersion.java");
        versionClass.parentFile.mkdirs();
        versionClass.text = """package io.littlehorse.server;

public class ServerVersion {
    public static final String VERSION = "${project.version}";
}
"""
    }
}

sourceSets {
  main {
    java {
        srcDir("$buildDir/generated/sources/version")
    }
  }
}

compileJava.dependsOn staticVersion
